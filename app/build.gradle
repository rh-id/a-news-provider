plugins {
    id 'com.android.application'
    id "com.cookpad.android.plugin.license-tools" version "1.2.6"
}

def keyStoreFile = File.createTempFile("key", null)
keyStoreFile.deleteOnExit()

if (System.getenv("SIGNING_KEY") != null) {
    println 'key exist'
    keyStoreFile.bytes = Base64.decoder.decode(System.getenv("SIGNING_KEY"))
} else {
    println 'key NOT exist'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    signingConfigs {
        release {
            storeFile keyStoreFile
            storePassword System.getenv("KEY_STORE_PASSWORD")
            keyAlias System.getenv("ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "m.co.rh.id.a_news_provider"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 59
        versionName "1.9.7"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled = true

        testInstrumentationRunner "m.co.rh.id.a_news_provider.test.AppAndroidJunit4Runner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (System.getenv("SIGNING_KEY") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding false
    }
    lintOptions {
        ignore("UseCompatLoadingForDrawables")
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation project(path: ':base')
    implementation project(path: ':component-network')

    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.squareup.leakcanary:plumber-android:2.9.1'
    implementation 'com.github.rh-id:AndroidShowCase:v0.0.1'

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime:$work_version"

    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:3.12.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}

project.afterEvaluate {
    def versionCode = android.defaultConfig.versionCode
    File changeLogTxt = new File(project.rootDir, "fastlane/metadata/android/en-US/changelogs/" + versionCode + ".txt")
    if (changeLogTxt.exists()) {
        File outputChangeLog = new File(project.buildDir, "changelog.txt")
        if (!outputChangeLog.exists()) {
            outputChangeLog.parentFile.mkdirs()
            outputChangeLog.createNewFile()
        }
        outputChangeLog.bytes = changeLogTxt.bytes
    }
}